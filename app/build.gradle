apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def VERSION_NAME = project.VERSION_NAME
def VERSION_CODE = Integer.parseInt(project.VERSION_CODE)

def ANDROID_COMPILE_SDK_VERSION = Integer.parseInt(project.ANDROID_COMPILE_SDK_VERSION)
def ANDROID_TARGET_SDK_VERSION = Integer.parseInt(project.ANDROID_TARGET_SDK_VERSION)
def ANDROID_BUILD_TOOLS_VERSION = project.ANDROID_BUILD_TOOLS_VERSION
def ANDROID_MIN_SDK_VERSION = Integer.parseInt(project.ANDROID_MIN_SDK_VERSION)

def JAVA_COMPATIBILITY =  JavaVersion.valueOf(project.JAVA_COMPATIBILITY)

android {
    compileSdkVersion ANDROID_COMPILE_SDK_VERSION
    buildToolsVersion ANDROID_BUILD_TOOLS_VERSION

    signingConfigs {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def ksPath = properties.getProperty('KS_PATH')
        def keystorePropertiesFile = ksPath == null ? rootProject.file("../keystore.properties") : new File(ksPath)
        if (!keystorePropertiesFile.exists())
            keystorePropertiesFile = rootProject.file("../keystore.properties")
        if (keystorePropertiesFile.exists()) {
            println "ksPath = ${ksPath}ï¼Œusing ${keystorePropertiesFile.absolutePath}"
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            config {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        } else {
            println "no valid ksPath, ignore it."
        }
    }
    defaultConfig {
        applicationId "com.obsez.android.lib.smbfilechooser.demo"
        minSdkVersion ANDROID_MIN_SDK_VERSION
        targetSdkVersion ANDROID_TARGET_SDK_VERSION
        versionCode VERSION_CODE
        versionName VERSION_NAME
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            multiDexEnabled false
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JAVA_COMPATIBILITY
        targetCompatibility JAVA_COMPATIBILITY
    }
    lintOptions {
        lintConfig rootProject.file('lint.xml')
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'  // Some libraries have issues with this.
        disable 'OldTargetApi'    // Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'IconDensities'   // For testing purpose. This is safe to remove.
        checkReleaseBuilds false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':library')
    implementation 'androidx.appcompat:appcompat:1.1.0-beta01'
    implementation 'eu.agno3.jcifs:jcifs-ng:2.1.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta1'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    debugImplementation 'androidx.multidex:multidex:2.0.1'

    /** LeakCanary - https://github.com/square/leakcanary */
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-2'
}
repositories {
    mavenCentral()
}
